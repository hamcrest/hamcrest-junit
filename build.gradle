apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

apply plugin: 'eclipse'

archivesBaseName = "hamcrest-junit"
group = "org.hamcrest"
version = hasProperty("-version") ? property("-version") : "SNAPSHOT"

def junit = System.getenv("junit") ?: 'junit:junit:4.12'
def hamcrest = System.getenv("hamcrest") ?: 'org.hamcrest:java-hamcrest:2.0.0.0'

sourceCompatibility = "1.7"

repositories {
    mavenCentral()
}

dependencies {
    compile (junit)    { transitive = false }
    compile (hamcrest) { transitive = false }
}

test {
    filter {
        includeTestsMatching "*Test"
    }
}

jar {
    manifest {
        attributes 'Implementation-Title': 'hamcrest-junit',
                'Implementation-Vendor': 'hamcrest.org',
                'Implementation-Version': version
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc
}

artifacts {
    archives jar, sourcesJar, javadocJar
}


signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

String publishUsername = hasProperty("ossrhUsername") ? property("ossrhUsername") : ""
String publishPassword = hasProperty("ossrhPassword") ? property("ossrhPassword") : ""

uploadArchives {
    repositories.mavenDeployer {
        beforeDeployment { deployment -> signing.signPom(deployment) }

        repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
            authentication(userName: publishUsername, password: publishPassword)
        }
        snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
            authentication(userName: publishUsername, password: publishPassword)
        }

        pom.project {
            name 'Hamcrest JUnit'
            packaging 'jar'
            description 'Classes to use Hamcrest matchers within JUnit tests'
            url 'https://github.com/hamcrest/hamcrest-junit'

            scm {
                connection 'git@github.com:hamcrest/hamcrest-junit.git'
                url 'https://github.com/hamcrest/hamcrest-junit'
            }

            licenses {
                license {
                    name 'Eclipse Public License - v 1.0'
                    url 'http://opensource.org/licenses/EPL-1.0'
                }
            }

            developers {
                developer {
                    id 'npryce'
                    name 'Nat Pryce'
                }
                developer {
                    id 'sf105'
                    name 'Steve Freeman'
                }
            }
        }
    }
}
